plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'java'
}

group = 'net.milkbowl'
version = '2.1.0'
description = 'Vault'
compileJava.options.encoding = 'UTF-8'
java.sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    // Maven repositories
    mavenCentral()
    mavenLocal()

    // Local libraries
    flatDir {
        dirs 'libs'
    }

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
        name 'Spigot Snapshots'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
        name 'Sonatype Snapshots'
    }

    maven {
        url 'https://oss.sonatype.org/content/repositories/central'
        name 'Sonatype Central'
    }
}

dependencies {
    // Spigot API - necessary for project
    // https://hub.spigotmc.org/nexus/content/repositories/snapshots
    compileOnly group: 'org.spigotmc', name: 'spigot-api', version: '1.20.4-R0.1-SNAPSHOT'

    // JetBrains Annotations - for code inspection and documentation
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation group: 'org.jetbrains', name: 'annotations', version: '24.1.0'

    // Lombok - for reducing boilerplate code
    // https://projectlombok.org
    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
}

compileJava {
    dependsOn('clean')
}

shadowJar {
    archiveFileName = "${project.name}-${version}.jar"

    dependencies {
        exclude(dependency(group: 'org.projectlombok', name: 'lombok'))
    }

    minimize()
    relocate 'org.jetbrains', 'net.milkbowl.vault.shaded.org.jetbrains'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            artifact shadowJar
        }
    }

    repositories {
        mavenLocal()
    }
}

// Explicitly configure task dependencies
tasks.named('publishMavenJavaPublicationToMavenLocal').configure {
    dependsOn shadowJar
}
